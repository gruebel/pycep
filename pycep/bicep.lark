start: element*

element: "targetScope" "=" QUOTED_INTERPOLATION                             -> scope
       | [decorator] "param" string data_type ["=" param_value]             -> param
       | "var" string "=" var_value                                         -> var
       | "output" string data_type "=" output_value                         -> output
       | [decorator] "resource" string type_api_pair "=" resource_value     -> resource
       | [decorator] "module" string module_path "=" module_value           -> module

?value: SIGNED_INT                                  -> int
      | "true"                                      -> true
      | "false"                                     -> false
      | QUOTED_INTERPOLATION                        -> string
      | FUNCTION                                    -> string
      | string
      | array
      | object

// element values

?param_value: value
            | MULTI_LINE_STRING                     -> multi_line_string

?var_value: param_value | loop | conditional

?output_value: param_value | loop | conditional

?resource_value: object | loop

?module_value: object | loop

// element type extras

data_type: STRING

type_api_pair: QUOTED_INTERPOLATION

module_path: QUOTED_INTERPOLATION

// loops

loop: "[" "for" (loop_index | loop_array | loop_array_index | loop_object) ":" ["if" "(" string ")"] value "]"

loop_index: STRING "in" "range" "(" STRING "," STRING ")"

loop_array: STRING "in" STRING

loop_array_index: "(" STRING "," STRING ")" "in" STRING

loop_object: STRING "in" "items" "(" STRING ")"

// data types

string: STRING

array: "[" value* "]"

object: "{" pair* "}"

pair: key ":" (value | loop | conditional)

key: STRING | QUOTED_INTERPOLATION

// decorators

decorator: ("@" (deco_allowed | deco_batch | deco_description | deco_min_len | deco_max_len | deco_min_val | deco_max_val | deco_metadata | deco_secure))+

deco_allowed: "allowed" "(" array ")"

deco_batch: "batchSize" "(" /\d+/ ")"

deco_description: "description" "(" QUOTED_INTERPOLATION ")"

deco_min_len: "minLength" "(" /\d+/ ")"

deco_max_len: "maxLength" "(" /\d+/ ")"

deco_min_val: "minValue" "(" /\d+/ ")"

deco_max_val: "maxValue" "(" /\d+/ ")"

deco_metadata: "metadata" "(" object ")"

deco_secure: "secure" "(" ")"

// operators

conditional: "("? (STRING | FUNCTION) ")"? "?" param_value ":" param_value

// Terms

STRING: /[\w_.]+/
FUNCTION: /(?!if\()(?=[\w_]+([\[\]]|[()])+)[\w_\-\[\]()\/.,'= ]+/
QUOTED_INTERPOLATION: "'" /[\s\w\-\/${}\[\]().,:+*@"]*/ "'"

MULTI_LINE_STRING: "'''" (/./ | NEWLINE)+ "'''"

STRING_INTERPOLATION : "${" /[^}]+/ "}"

%import common.CPP_COMMENT
%import common.NEWLINE
%import common.SIGNED_INT
%import common.WS
%ignore CPP_COMMENT
%ignore NEWLINE
%ignore WS
