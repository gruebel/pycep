start: ((element _CPP_COMMENT_NL?) | _CPP_COMMENT_NL)*

element: "targetScope" "=" QUOTED_INTERPOLATION                                         -> scope
       | "metadata" STRING "=" metadata_value                                           -> metadata
       | [decorator] "param" STRING data_type ["=" value_brackets]                      -> param
       | [decorator] "var" STRING "=" var_value                                         -> var
       | [decorator] "output" STRING data_type "=" output_value                         -> output
       | [decorator] "resource" STRING type_api_pair [EXISTING] "=" resource_value      -> resource
       | [decorator] "module" STRING module_path "=" module_value                       -> module
       | [decorator] "type" STRING "=" type_value                                       -> custom_type

?value: _NEWLINE? "null"                      -> null
      | _NEWLINE? "true"                      -> true
      | _NEWLINE? "false"                     -> false
      | _NEWLINE? INT                         -> int
      | _NEWLINE? STRING                      -> string
      | _NEWLINE? QUOTED_INTERPOLATION        -> string
      | MULTI_LINE_STRING                     -> multi_line_string
      | array
      | object
      | _NEWLINE? function
      | operator

?value_brackets: (value | "(" value ")") "!"?

// element values

?var_value: value_brackets | loop

?metadata_value: QUOTED_INTERPOLATION -> string

?output_value: value_brackets | loop

?resource_value: object | loop | deploy_condition

?module_value: object | loop | deploy_condition

child_resource: "resource" STRING QUOTED_INTERPOLATION [EXISTING] "=" object _CPP_COMMENT_NL+

?type_value: value_brackets ("|" value_brackets)*

// element type extras

data_type: STRING

type_api_pair: QUOTED_INTERPOLATION

module_path: QUOTED_INTERPOLATION

// loops

loop: "[" "for" (loop_array | loop_array_index | loop_object | loop_range) ":" ["if" "(" value_brackets ")"] value_brackets "]"

loop_array: STRING "in" value_brackets

loop_array_index: "(" STRING "," STRING ")" "in" value_brackets

loop_object: STRING "in" "items" "(" value_brackets ")"

loop_range: ["(" STRING ","] STRING ")"? "in" "range" "(" value_brackets "," value_brackets ")"

// deploy condition

deploy_condition: "if" "(" value_brackets ")" value_brackets

// data types

?quoted_string: STRING | QUOTED_STRING | QUOTED_INTERPOLATION

array: "[" _CPP_COMMENT_NL~0..10 (value_brackets ("," value_brackets)* _CPP_COMMENT_NL~0..5)* "]"

object: "{" _CPP_COMMENT_NL~0..10 ((pair ("," pair)* | child_resource) _CPP_COMMENT_NL~0..5)* "}"

pair: key ":" (value_brackets | loop) "?"?

!key: "resource" | quoted_string

// decorators

decorator: ("@" "sys."? (deco_allowed | deco_batch | deco_description | deco_min_len | deco_max_len | deco_min_val | deco_max_val | deco_metadata | deco_secure) _CPP_COMMENT_NL~0..5)+

deco_allowed: "allowed" "(" array ")"

deco_batch: "batchSize" "(" /\d+/ ")"

deco_description: "description" "(" (QUOTED_INTERPOLATION | MULTI_LINE_STRING) ")"

deco_min_len: "minLength" "(" /\d+/ ")"

deco_max_len: "maxLength" "(" /\d+/ ")"

deco_min_val: "minValue" "(" /-?\d+/ ")"

deco_max_val: "maxValue" "(" /\d+/ ")"

deco_metadata: "metadata" "(" _CPP_COMMENT_NL? object _CPP_COMMENT_NL? ")"

deco_secure: "secure" "(" ")"

// functions

function: function_any | function_array | function_date | function_deployment | function_file | function_lambda
        | function_logical | function_numeric | function_object | function_resource | function_scope | function_string

// functions - any

?function_any: any_func

any_func: ("any" | "sys.any") "(" value_brackets ")" ["." STRING]

// functions - array

?function_array: array_func | concat | contains | empty | first | flatten | intersection | last | length | max_func | min_func
               | skip | take | union

_ARRAY.1: ("array" | "sys.array") "("
array_func: _ARRAY value_brackets ")"

concat: ("concat" | "sys.concat") "(" value_brackets ("," value_brackets)* ")"

contains: ("contains" | "sys.contains") "(" value_brackets "," value_brackets ")"

empty: ("empty" | "sys.empty") "(" value_brackets ")"

first: ("first" | "sys.first") "(" value_brackets ")" ["." STRING]

flatten: ("flatten" | "sys.flatten") "(" value_brackets ")" ["." STRING]

intersection: ("intersection" | "sys.intersection") "(" value_brackets ("," value_brackets)+ ")" ["." STRING]

last: ("last" | "sys.last") "(" value_brackets ")" ["." STRING]

_LENGTH.1: ("length" | "sys.length") "("
length: _LENGTH value_brackets ")"

_MAX.1: ("max" | "sys.max") "("
max_func: _MAX value_brackets ("," value_brackets)* ")"

_MIN.1: ("min" | "sys.min") "("
min_func: _MIN value_brackets ("," value_brackets)* ")"

skip: ("skip" | "sys.skip") "(" value_brackets "," value_brackets ")"

take: ("take" | "sys.take") "(" value_brackets "," value_brackets ")"

union: ("union" | "sys.union") "(" value_brackets ("," _CPP_COMMENT_NL? value_brackets)+ _CPP_COMMENT_NL? ")" ["." STRING]

// functions - date

?function_date: date_time_add | date_time_from_epoch | date_time_to_epoch | utc_now

date_time_add: ("dateTimeAdd" | "sys.dateTimeAdd") "(" value_brackets "," value_brackets ["," value_brackets]  ")"

date_time_from_epoch: ("dateTimeFromEpoch" | "sys.dateTimeFromEpoch") "(" value_brackets ")"

date_time_to_epoch: ("dateTimeToEpoch" | "sys.dateTimeToEpoch") "(" value_brackets ")"

utc_now: ("utcNow" | "sys.utcNow") "(" [value_brackets] ")"

// functions - deployment

?function_deployment: deployment | environment

_DEPLOYMENT.1: ("deployment" | "az.deployment") "("
deployment: _DEPLOYMENT ")" ["." STRING]

_ENVIRONMENT.1: ("environment" | "az.environment") "("
environment: _ENVIRONMENT ")" ["." STRING]

// functions - file

?function_file: load_file_as_base64 | load_json_content | load_yaml_content | load_text_content

load_file_as_base64: ("loadFileAsBase64" | "sys.loadFileAsBase64") "(" value_brackets ")"

load_json_content: ("loadJsonContent" | "sys.loadJsonContent") "(" value_brackets ["," value_brackets ["," value_brackets]] ")" [["[" STRING "]"] "." (STRING | INDEXED)]

load_yaml_content: ("loadYamlContent" | "sys.loadYamlContent") "(" value_brackets ["," value_brackets ["," value_brackets]] ")" [["[" STRING "]"] "." (STRING | INDEXED)]

load_text_content: ("loadTextContent" | "sys.loadTextContent") "(" value_brackets ["," value_brackets] ")"

// functions - lambda

?function_lambda: filter

filter: ("filter" | "sys.filter") "(" value_brackets "," STRING "=>" value_brackets ")"

// functions - logical

?function_logical: bool_func

_BOOL.1: ("bool" | "sys.bool") "("
bool_func: _BOOL value_brackets ")"

// functions - numeric

?function_numeric: int_func

_INT.1: ("int" | "sys.int") "("
int_func: _INT value_brackets ")"

// functions - object

?function_object: json_func

_JSON.1: ("json" | "sys.json") "("
json_func: _JSON value_brackets ")" [["[" STRING "]"] "." (STRING | INDEXED)]

// functions - resource

?function_resource: extension_resource_id | list_func | management_group_resource_id | pick_zones | reference
                  | resource_id | subscription_resource_id | tenant_resource_id

extension_resource_id: ("extensionResourceId" | "az.extensionResourceId") "(" value_brackets ("," value_brackets)~2..3 ")"

// See https://learn.microsoft.com/en-us/azure/azure-resource-manager/bicep/bicep-functions-resource#list
// With Bicep v0.4.412+, you call list*() using accessor operator, ex: storageAccount.listKeys()
list_func: (LIST_FUNC_NAME | "az." LIST_FUNC_NAME) "(" value_brackets "," value_brackets ")"

management_group_resource_id: ("managementGroupResourceId" | "az.managementGroupResourceId") "(" value_brackets ("," value_brackets)~1..2 ")"

pick_zones: ("pickZones" | "az.pickZones") "(" value_brackets ("," value_brackets)~2..4 ")"

reference: ("reference" | "az.reference") "(" value_brackets ["," value_brackets ["," value_brackets]] ")" ["." STRING]

_RESOURCE_ID.1: ("resourceId" | "az.resourceId") "("
resource_id: _RESOURCE_ID value_brackets ("," value_brackets)~1..9 _CPP_COMMENT_NL? ")"

subscription_resource_id: ("subscriptionResourceId" | "az.subscriptionResourceId") "(" value_brackets ("," value_brackets)~1..3 ")"

tenant_resource_id: ("tenantResourceId" | "az.tenantResourceId") "(" value_brackets ("," value_brackets)~1..2 ")"

// functions - scope

?function_scope: management_group | resource_group | subscription | tenant

_MANAGEMENT_GROUP.1: ("managementGroup" | "az.managementGroup") "("
management_group: _MANAGEMENT_GROUP [value_brackets] ")" ["." STRING]

_RESOURCE_GROUP.1: ("resourceGroup" | "az.resourceGroup") "("
resource_group: _RESOURCE_GROUP [value_brackets ","? [value_brackets]] ")" ["." STRING]

_SUBSCRIPTION.1: ("subscription" | "az.subscription") "("
subscription: _SUBSCRIPTION [value_brackets] ")" ["." STRING]

_TENANT.1: ("tenant" | "az.tenant") "("
tenant: _TENANT ")" ["." STRING]

// functions - string

?function_string: base64_func | base64_to_json | base64_to_string | data_uri | data_uri_to_string | ends_with | format
                | guid | index_of | last_index_of | new_guid | pad_left | replace | split | join | starts_with | string_func
                | substring | to_lower | to_upper | trim | unique_string | uri | uri_component | uri_component_to_string

base64_func: ("base64" | "sys.base64") "(" value_brackets ")"

base64_to_json: ("base64ToJson" | "sys.base64ToJson") "(" value_brackets ")"

base64_to_string: ("base64ToString" | "sys.base64ToString") "(" value_brackets ")"

data_uri: ("dataUri" | "sys.dataUri") "(" value_brackets ")"

data_uri_to_string: ("dataUriToString" | "sys.dataUriToString") "(" value_brackets ")"

ends_with: ("endsWith" | "sys.endsWith") "(" value_brackets "," value_brackets ")"

_FORMAT.1: ("format" | "sys.format") "("
format: _FORMAT value_brackets ("," value_brackets)+ ")"

_GUID.1: ("guid" | "sys.guid") "("
guid: _GUID value_brackets ("," value_brackets)* ")"

index_of: ("indexOf" | "sys.indexOf") "(" value_brackets "," value_brackets ")"

last_index_of: ("lastIndexOf" | "sys.lastIndexOf") "(" value_brackets "," value_brackets ")"

new_guid: ("newGuid" | "sys.newGuid") "(" ")"

pad_left: ("padLeft" | "sys.padLeft") "(" value_brackets "," value_brackets ["," value_brackets] ")"

replace: ("replace" | "sys.replace") "(" value_brackets ("," value_brackets)~2 ")"

split: ("split" | "sys.split") "(" value_brackets "," value_brackets ")" ["[" INT "]"]

join: ("join" | "sys.join") "(" value_brackets "," value_brackets ")"

starts_with: ("startsWith" | "sys.startsWith") "(" value_brackets "," value_brackets ")"

_STRING.1: ("string" | "sys.string") "("
string_func: _STRING value_brackets ")"

substring: ("substring" | "sys.substring") "(" value_brackets "," value_brackets ["," value_brackets] ")"

to_lower: ("toLower" | "sys.toLower") "(" value_brackets ")"

to_upper: ("toUpper" | "sys.toUpper") "(" value_brackets ")"

trim: ("trim" | "sys.trim") "(" value_brackets ")"

unique_string: ("uniqueString" | "sys.uniqueString") "(" value_brackets ("," value_brackets)* ")"

_URI.1: ("uri" | "sys.uri") "("
uri: _URI value_brackets "," value_brackets ")"

uri_component: ("uriComponent" | "sys.uriComponent") "(" value_brackets ")"

uri_component_to_string: ("uriComponentToString" | "sys.uriComponentToString") "(" value_brackets ")"

// operators

operator: operator_comparison | operator_logical | operator_numeric | operator_accessor

// operators - comparison

?operator_comparison: greater_than_or_equals | greater_than | less_than_or_equals | less_than
                    | equals | not_equals | equals_case_insens | not_equals_case_insens

greater_than_or_equals: value_brackets ">=" value_brackets

greater_than: value_brackets ">" value_brackets

less_than_or_equals: value_brackets "<=" value_brackets

less_than: value_brackets "<" value_brackets

equals: value_brackets "==" value_brackets

not_equals: value_brackets "!=" value_brackets

equals_case_insens: value_brackets "=~" value_brackets

not_equals_case_insens: value_brackets "!~" value_brackets

// operators - logical

?operator_logical: and_op | or_op | not_op | coalesce | conditional

and_op: value_brackets "&&" value_brackets

or_op: value_brackets "||" value_brackets

not_op: "!" value_brackets

coalesce: value_brackets "??" value_brackets

conditional: value_brackets "?" value_brackets ":" value_brackets

// operators - numeric

?operator_numeric: add | divide | minus | modulo | multiply | substract

add: value_brackets "+" value_brackets

divide: value_brackets "/" value_brackets

minus: "-" value_brackets

modulo: value_brackets "%" value_brackets

multiply: value_brackets "*" value_brackets

substract.1: value_brackets "-" value_brackets

// operators - accessor

?operator_accessor: index_accessor | function_accessor | nested_resource_accessor | property_accessor

index_accessor: value_brackets "[" value_brackets "]"

function_accessor: value_brackets "." LIST_FUNC_NAME "(" value_brackets? ["," value_brackets]+ ")"

nested_resource_accessor: value_brackets "::" value_brackets

// 'sys.' and 'az.' have to be eliminated in 1st operand (see STRING definition regexp):
property_accessor: value_brackets "." value_brackets

// Terms

EXISTING: "existing"

STRING: /(?!(sys|az)\.)[a-zA-Z_]([\w_])*/
QUOTED_STRING: "'" /[a-zA-Z_]([\w_.])*/ "'"
INDEXED: /(?=[\w][\w_.]+(.(get|list)[a-zA-Z]*\(('[\w-]+'|[\w.]+)?\)|[\[\]])+)(.(get|list)[a-zA-Z]*\(('[\w-]+'|[\w.]+)?\)|\[[\w\[\]_().']+\]|[\w_\-\/.,'= ])+/
QUOTED_INTERPOLATION: /(?!.*''')'(\${(?:[^}'{]+|(?R))*+}|\\'|[^'])*'/
LIST_FUNC_NAME: /(?=([Ll]ist[A-za-z]*|getSecret)\()([A-Za-z]+)/

MULTI_LINE_STRING: "'''" (/.*?/ | NEWLINE)+ "'''"

_NEWLINE: NEWLINE
_CPP_COMMENT_NL: [CPP_COMMENT] NEWLINE

%import common.C_COMMENT
%import common.CPP_COMMENT
%import common.SH_COMMENT
%import common.INT
%import common.NEWLINE
%import common.WS_INLINE

%ignore C_COMMENT
%ignore SH_COMMENT
%ignore WS_INLINE
